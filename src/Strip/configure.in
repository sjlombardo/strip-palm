dnl
dnl $Id: configure.in,v 1.3 2005/08/20 17:51:06 lombardo Exp $
dnl
dnl Strip autoconf input
dnl
dnl

AC_INIT(Strip.c)

VERSION=2.0
PACKAGE=Strip

AC_SUBST(VERSION)
AC_SUBST(PACKAGE)

dnl =======================================
dnl                OPTIONS
dnl =======================================
TYPE=
AC_ARG_ENABLE(palmos, [  --enable-palmos          use Palm's version of prc-tools ], TYPE=palmos)
AC_ARG_ENABLE(palmos-coff, [  --enable-palmos-coff        use A4 based version of prc-tools ], TYPE=palmos-coff)

SALT=
ITERATIONS=
REDUCTIONS=

AC_ARG_WITH(salt, [ --with-salt=SALT		salt for registration code ], [ SALT="\"\\\"$withval\\\"\"" ], [ SALT="\"\\\"ZETETIC\\\"\"" ] )
AC_ARG_WITH(iterations, [ --with-iterations=ITERATIONS		iterations for registration code ], [ ITERATIONS="$withval" ], [ ITERATIONS="1" ] )
AC_ARG_WITH(reductions, [ --with-reductions=REDUCTIONS		reductions for registration code ], [ REDUCTIONS="$withval" ], [ REDUCTIONS=2 ] )

DEFINES="$DEFINES -DREG_SALT=$SALT -DREG_ITERATIONS=$ITERATIONS -DREG_REDUCTIONS=$REDUCTIONS"

if test "X$TYPE" = "X"; then
	AC_PATH_PROGS(GCC, m68k-palmos-gcc m68k-palmos-coff-gcc)
	if test "X$GCC" = "X"; then
	        AC_MSG_ERROR("Unable to find cross-compiler")
	fi
	TMP=`basename $GCC`
	if test "X$TMP" = "Xm68k-palmos-gcc"; then
		TYPE=palmos
	else
		TYPE=palmos-coff
	fi
else
	AC_PATH_PROGS(GCC, m68k-$TYPE-gcc)
	if test "X$GCC" = "X"; then
	        AC_MSG_ERROR("Unable to find cross-compiler")
	fi
fi

if test "$TYPE" = "palmos"; then
	DEFINES="$DEFINES"
	SECTION=strip_db
	SEGMENTDEF=Strip.def
	MULTISEG_LD=Strip-sections.ld
	MULTISEG_S=Strip-sections.s
	cat <<-EOF >$SEGMENTDEF

	application { "Strip" "SJLO" }
	multiple code { $SECTION }

	EOF
else
	DEFINES="$DEFINES -DHAVE_GDBHOOK"
	SEGMENTDEF=strip_db
	MULTISEG_LD=multiseg.ld
	MULTISEG_S=multisegstub.s
fi
AC_SUBST(TYPE)
AC_SUBST(DEFINES)
AC_SUBST(SEGMENTDEF)
AC_SUBST(MULTISEG_LD)
AC_SUBST(MULTISEG_S)

dnl -------------------------------------------------------------------------------------
dnl Checking for the necessary Palm development tools
dnl -------------------------------------------------------------------------------------

AC_PATH_PROGS(OBJRES, m68k-$TYPE-obj-res obj-res)
if test "X$OBJRES" = "X"; then
        AC_MSG_ERROR("Unable to find obj-res")
fi
AC_PATH_PROGS(AS, m68k-$TYPE-as)
if test "X$AS" = "X"; then
        AC_MSG_ERROR("Unable to find assembler")
fi
if test "$TYPE" = "palmos"; then
        STUBGEN=m68k-$TYPE-multigen
else
        STUBGEN=m68k-$TYPE-multisegstubgen
fi
AC_PATH_PROGS(MULTISEGSTUBGEN, $STUBGEN)
if test "X$MULTISEGSTUBGEN" = "X"; then
        AC_MSG_ERROR("Unable to find $STUBGEN")
fi
AC_PATH_PROG(PILRC, pilrc)
if test "X$PILRC" = "X"; then
        AC_MSG_ERROR("Unable to find pilrc")
fi
AC_PATH_PROG(BUILD_PRC, build-prc)
if test "X$BUILD_PRC" = "X"; then
        AC_MSG_ERROR("Unable to find build-prc")
fi
AC_PATH_PROG(HOST_CC, gcc)
if test "X$HOST_CC" = "X"; then
	AC_MSG_ERROR("Unable to find host gcc")
fi
AC_PATH_PROG(PILOT_XFER, pilot-xfer)
if test "X$PILOT_XFER" = "X"; then
	AC_MSG_WARN("Unable to find pilot-xfer")
fi

AC_PATH_PROG(CTAGS, ctags, ctags)
AC_PATH_PROG(AUTOCONF, autoconf, autoconf)

dnl ===========================================================================

AC_OUTPUT(Makefile)

dnl ===========================================================================
